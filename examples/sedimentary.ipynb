
{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   
   "source": [
    "[![Script](https://acm.im/bristlecone//img/badge-script.svg)](https://acm.im/bristlecone//examples/sedimentary.fsx)\u0026emsp;\n",
    "[![Notebook](https://acm.im/bristlecone//img/badge-notebook.svg)](https://acm.im/bristlecone//examples/sedimentary.ipynb)\n",
    "\n",
    "## The long-term ecological detective: Holocene ecosystem functioning\n",
    "\n",
    "Analysis of microfossils and inorganic compounds from lake sediment\n",
    "cores and peat cores can provide rich information on past biodiversity\n",
    "and environmental conditions.\n",
    "\n",
    "[Jeffers et al (2011)](https://nph.onlinelibrary.wiley.com/doi/full/10.1111/j.1469-8137.2011.03907.x)\n",
    "identified ...\n",
    "\n",
    "To get started, we first load and open the Bristlecone library in\n",
    "an F# script file (.fsx):\n",
    "\n"
   ]
  }
,
  {
   "cell_type": "code",
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "execution_count": 2, "outputs": [],
   "source": [
    "open Bristlecone\n",
    "open Bristlecone.Language\n",
    "open Bristlecone.Time\n"
   ]
  }
,
  {
   "cell_type": "markdown",
   "metadata": {},
   
   "source": [
    "Then, we may define units of measure that are not SI units (which are included with F#)\n",
    "and are not provided in the Time module of Bristlecone.\n",
    "\n"
   ]
  }
,
  {
   "cell_type": "code",
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "execution_count": 3, "outputs": [],
   "source": [
    "[\u003cMeasure\u003e] type area\n",
    "[\u003cMeasure\u003e] type indiv // pollen accumulation proxy units -\u003e individuals per area\n",
    "[\u003cMeasure\u003e] type conc  // nitrogen concentration proxy units (e.g., δ15N or %TN, scaled)\n",
    "[\u003cMeasure\u003e] type d15N // proxy measure of nitrogen\n",
    "[\u003cMeasure\u003e] type grain\n",
    "[\u003cMeasure\u003e] type cm\n"
   ]
  }
,
  {
   "cell_type": "markdown",
   "metadata": {},
   
   "source": [
    "We can then use these units of measure when defining the model system.\n",
    "Before we can write model equations, we need to define the states, parameters,\n",
    "and measures that we need to apply within them.\n",
    "\n"
   ]
  }
,
  {
   "cell_type": "code",
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "execution_count": 4, "outputs": [],
   "source": [
    "// States\n",
    "let N = state\u003cconc\u003e \"available_N\"         // Reconstructed available N proxy (δ15N or %TN scaled)\n",
    "let X = state\u003cindiv / area\u003e \"population\"  // Pollen accumulation proxy for population density\n",
    "let obsN = measure \"observed_N\" // After conversion with an alpha conversion factor\n",
    "let obsPAR = measure\u003c(grain / cm^2) / year\u003e \"observed_PAR\"\n",
    "\n",
    "// Core ecological parameters\n",
    "let λ = parameter \"λ\" notNegative 0.01\u003cconc/year\u003e 1.0\u003cconc/year\u003e // External N input\n",
    "let γN = parameter \"γ[N]\" notNegative 0.001\u003c1/year\u003e 0.1\u003c1/year\u003e // N loss rate\n",
    "let r   = parameter \"r\" notNegative 0.001\u003c(indiv/area)/conc\u003e 20.\u003c(indiv/area)/conc\u003e // Intrinsic growth rate\n",
    "let γX = parameter \"γ[X]\" notNegative 0.01\u003c1/year\u003e 0.2\u003c1/year\u003e // mortality\n",
    "\n",
    "// Measurement model parameters\n",
    "let αδ15N = parameter\u003cd15N\u003e \"αδ15N\" noConstraints -2.0\u003cd15N\u003e 2.0\u003cd15N\u003e   // intercept for δ15N proxy\n",
    "let βδ15N = parameter\u003cd15N/conc\u003e \"βδ15N\" noConstraints 0.1\u003cd15N/conc\u003e 2.0\u003cd15N/conc\u003e    // slope linking latent N to δ15N\n",
    "\n",
    "// Likelihood function parameters\n",
    "let ρ = parameter \"ρ\" noConstraints -0.500 0.500\n",
    "let σx = parameter \"σ[x]\" notNegative 10. 50.\n",
    "let σy = parameter \"σ[y]\" notNegative 0.001 0.100\n"
   ]
  }
,
  {
   "cell_type": "markdown",
   "metadata": {},
   
   "source": [
    "In this scenario, we will assess eight hypotheses that relate to the form of\n",
    "dependency between an individual plant taxon and nitrogen availability. To\n",
    "scaffold the hypotheses, we first define a base model that contains the two\n",
    "ordinary differential equations for the two state variables; it takes three\n",
    "pluggable model components as arguments using a standard F# function definition.\n",
    "\n"
   ]
  }
,
  {
   "cell_type": "code",
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "execution_count": 5, "outputs": [],
   "source": [
    "let baseModel\n",
    "    (uptake  : ModelExpression\u003cconc\u003e -\u003e ModelExpression\u003cindiv/area\u003e -\u003e ModelExpression\u003cconc/year\u003e * ModelExpression\u003c1\u003e)\n",
    "    (feedback: ModelExpression\u003cindiv/area\u003e -\u003e ModelExpression\u003cconc/year\u003e)\n",
    "    (density : ModelExpression\u003cindiv/area\u003e -\u003e ModelExpression\u003c1\u003e) =\n",
    "\n",
    "    // ODEs\n",
    "    let ``dN/dt``: ModelExpression\u003cconc/year\u003e =\n",
    "        let uptake, uptakeMult = uptake This (State X)\n",
    "        P λ\n",
    "        - uptake * uptakeMult\n",
    "        - P γN * This\u003cconc\u003e\n",
    "        + feedback(State X)\n",
    "\n",
    "    let ``dX/dt``: ModelExpression\u003c(indiv / area)/year\u003e =\n",
    "        let uptake, _ = uptake (State N) This\n",
    "        P r * uptake * density This - P γX * This\n",
    "\n",
    "    // Measure: convert modeled N to proxy comparison scale\n",
    "    let nToProxy : ModelExpression\u003cd15N\u003e = P αδ15N + P βδ15N * State N\n",
    "    let nFromProxy : ModelExpression\u003cconc\u003e = (Measure obsN - P αδ15N) / P βδ15N\n",
    "\n",
    "    Model.empty\n",
    "    // Add the core ecological ODEs:\n",
    "    |\u003e Model.addRateEquation X ``dX/dt``\n",
    "    |\u003e Model.addRateEquation N ``dN/dt``\n",
    "    |\u003e Model.estimateParameter λ\n",
    "    |\u003e Model.estimateParameter γN\n",
    "    |\u003e Model.estimateParameter r\n",
    "    |\u003e Model.estimateParameter γX\n",
    "    // Add the conversion from d15N to N availability:\n",
    "    |\u003e Model.addMeasure obsN nToProxy\n",
    "    |\u003e Model.initialiseHiddenStateWith N nFromProxy\n",
    "    |\u003e Model.estimateParameter αδ15N\n",
    "    |\u003e Model.estimateParameter βδ15N\n",
    "    // Add the likelihood function:\n",
    "    |\u003e Model.useLikelihoodFunction (ModelLibrary.Likelihood.bivariateGaussian (Require.state X) (Require.measure obsN))\n",
    "    |\u003e Model.estimateParameter ρ\n",
    "    |\u003e Model.estimateParameter σx\n",
    "    |\u003e Model.estimateParameter σy\n",
    "    \n"
   ]
  }
,
  {
   "cell_type": "markdown",
   "metadata": {},
   
   "source": [
    "In this model, we naively assume that the pollen accumulation rate approximates the individuals of the\n",
    "plant taxon per unit area. However, for nitrogen the use of a measurement model is desirable, at a minimum\n",
    "because raw d15N (permil) does not\n",
    "\n",
    "Next, we define the three interchangable components that we will plug in. The models as stated in\n",
    "Jeffers (2011) are not truly nested, as some combinations are not ecologically plausable. In this\n",
    "instance, we define two sets of model components; one for N-dependent systems, and one for\n",
    "N-independent systems. The two sets are encoded in a record for each of the three modes below.\n",
    "\n",
    "#### Feedback component.\n",
    "\n",
    "A plant-soil feedback may be enabled or disabled, and is only defined in one\n",
    "mathematical form.\n",
    "\n"
   ]
  }
,
  {
   "cell_type": "code",
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "execution_count": 6, "outputs": [],
   "source": [
    "let feedbackMode =\n",
    "\n",
    "    // Conversion factor (individuals into N)\n",
    "    let σ  = parameter \"α\" notNegative 0.01\u003cconc/(indiv/area)\u003e 10.\u003cconc/(indiv/area)\u003e\n",
    "\n",
    "    let none _ = Constant 0.\u003cconc/year\u003e\n",
    "    let positive (X: ModelExpression\u003cindiv/area\u003e) = P σ * P γX * X\n",
    "\n",
    "    {|\n",
    "        NDependent =\n",
    "            Components.modelComponent \"Feedback\" [\n",
    "                Components.subComponent \"None\" none\n",
    "                Components.subComponent \"Positive feedback\" positive\n",
    "                |\u003e Components.estimateParameter σ\n",
    "            ]\n",
    "        NIndependent =\n",
    "            Components.modelComponent \"Feedback\" [\n",
    "                Components.subComponent \"Positive feedback\" positive\n",
    "                |\u003e Components.estimateParameter σ\n",
    "            ]\n",
    "    |}\n"
   ]
  }
,
  {
   "cell_type": "markdown",
   "metadata": {},
   
   "source": [
    "#### N-dependency\n",
    "\n",
    "We define the N-dependency (\"uptake\") as a tuple, where the first element\n",
    "is the form of uptake, and the second is a multiplier used to turn on or\n",
    "off the uptake term within the `dN/dt` equation. This is required because\n",
    "when growth is N-independent, the growth term in `dX/dt` must be multipled\n",
    "by 1, whereas uptake must be turned off. Three forms of N-dependency are\n",
    "specified: N-independent, linear N-dependency, and saturating N-dependency.\n",
    "\n"
   ]
  }
,
  {
   "cell_type": "code",
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "execution_count": 7, "outputs": [],
   "source": [
    "let uptakeMode =\n",
    "\n",
    "    // Parameters:\n",
    "    let a = parameter \"a\" notNegative 1e-6\u003carea/(indiv year)\u003e 1e-3\u003carea/(indiv year)\u003e // Uptake rate constant\n",
    "    let b = parameter \"b\" notNegative 0.5\u003c/conc\u003e 2.0\u003c/conc\u003e // Half-saturation (MM)\n",
    "\n",
    "    // If independent, need to substitute 1 into the growth equation instead of 0.\n",
    "    let independent _ _ =\n",
    "        Constant 1.\u003cconc/year\u003e, Constant 0.\n",
    "\n",
    "    // tuple of (uptake rate (0 if none), uptake multiplier)\n",
    "    let linear (N: ModelExpression\u003cconc\u003e) (X: ModelExpression\u003cindiv/area\u003e) = P a * N * X, Constant 1.\n",
    "    let michaelisMenten (N: ModelExpression\u003cconc\u003e) (X: ModelExpression\u003cindiv/area\u003e) : ModelExpression\u003cconc/year\u003e * ModelExpression\u003c1\u003e =\n",
    "        (P a * N * X) / (Constant 1. + (P b * N)), Constant 1.\n",
    "\n",
    "    {|\n",
    "        NDependent =\n",
    "            Components.modelComponent \"Uptake\" [\n",
    "                Components.subComponent \"Linear\" linear\n",
    "                |\u003e Components.estimateParameter a\n",
    "                Components.subComponent \"Saturating (Michaelis-Menten)\" michaelisMenten\n",
    "                |\u003e Components.estimateParameter a\n",
    "                |\u003e Components.estimateParameter b\n",
    "            ]\n",
    "        NIndependent =\n",
    "            Components.modelComponent \"Uptake\" [\n",
    "                Components.subComponent \"Independent\" independent\n",
    "            ]\n",
    "    |}\n"
   ]
  }
,
  {
   "cell_type": "markdown",
   "metadata": {},
   
   "source": [
    "#### Density-dependency\n",
    "\n",
    "...\n",
    "\n"
   ]
  }
,
  {
   "cell_type": "code",
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "execution_count": 8, "outputs": [],
   "source": [
    "let densityMode =\n",
    "\n",
    "    let K   = parameter \"K_growth\"   notNegative 1.\u003cindiv/area\u003e 1e6\u003cindiv/area\u003e\n",
    "    let c  = parameter \"c_density\"  notNegative 0.01\u003carea/indiv\u003e 1e-3\u003carea/indiv\u003e\n",
    "\n",
    "    let logisticDD (X: ModelExpression\u003cindiv/area\u003e) : ModelExpression\u003c1\u003e =\n",
    "        Constant 1.\u003c1\u003e - X / P K\n",
    "\n",
    "    let expDD (X: ModelExpression\u003cindiv/area\u003e) : ModelExpression\u003c1\u003e =\n",
    "        Exponential (-(P c) * X)\n",
    "\n",
    "    let constant (_: ModelExpression\u003cindiv/area\u003e) : ModelExpression\u003c1\u003e =\n",
    "        Constant 1.\u003c1\u003e\n",
    "\n",
    "    {|\n",
    "        NDepenentGrowth =\n",
    "            Components.modelComponent \"Density\" [\n",
    "                Components.subComponent \"Constant\" constant\n",
    "                Components.subComponent \"Logistic\" logisticDD\n",
    "                |\u003e Components.estimateParameter K\n",
    "            ]\n",
    "        NIndependentGrowth =\n",
    "            Components.modelComponent \"Density\" [\n",
    "                Components.subComponent \"Logistic\" logisticDD\n",
    "                |\u003e Components.estimateParameter K\n",
    "                Components.subComponent \"ExpDecay\" expDD\n",
    "                |\u003e Components.estimateParameter c\n",
    "            ]\n",
    "    |}\n"
   ]
  }
,
  {
   "cell_type": "markdown",
   "metadata": {},
   
   "source": [
    "Finally, we can scaffold all of the possible model-component combinations\n",
    "into a set of hypotheses that represents the product of all of the\n",
    "components.\n",
    "\n",
    "The system is not truly nested, so we create two nested sets\n",
    "and concatenate them together.\n",
    "\n"
   ]
  }
,
  {
   "cell_type": "code",
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "execution_count": 9, "outputs": [],
   "source": [
    "let hypothesesDependent =\n",
    "    Hypotheses.createFromModel baseModel\n",
    "    |\u003e Hypotheses.apply uptakeMode.NDependent\n",
    "    |\u003e Hypotheses.apply feedbackMode.NDependent\n",
    "    |\u003e Hypotheses.apply densityMode.NDepenentGrowth\n",
    "    |\u003e Hypotheses.compile\n",
    "\n",
    "let hypothesesIndependent =\n",
    "    Hypotheses.createFromModel baseModel\n",
    "    |\u003e Hypotheses.apply uptakeMode.NIndependent\n",
    "    |\u003e Hypotheses.apply feedbackMode.NIndependent\n",
    "    |\u003e Hypotheses.apply densityMode.NIndependentGrowth\n",
    "    |\u003e Hypotheses.compile\n",
    "\n",
    "let hypotheses =\n",
    "    List.append hypothesesDependent hypothesesIndependent\n"
   ]
  }
,
  {
   "cell_type": "markdown",
   "metadata": {},
   
   "source": [
    "The resulting list of hypotheses mirrors the 10 nitrogen-based models\n",
    "presented in Table 1 of Jeffers et al (2011). Bristlecone assigns each\n",
    "model a code based on the components within it. For our 10 models that\n",
    "vary uptake mechanism (UP), feedback (FE), and density-dependence (DE), they are:\n",
    "\n"
   ]
  }
,
  {
   "cell_type": "code",
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "execution_count": 10, "outputs": [
          {
           "data": {
            "text/plain": [""]
        },
           "execution_count": 10,
           "metadata": {},
           "output_type": "execute_result"
          }],
   "source": [
    "for h in hypotheses do\n",
    "    printfn \"%s\" h.ReferenceCode\n"
   ]
  }
,
  {
   "cell_type": "markdown",
   "metadata": {},
   
   "source": [
    "### Fitting the models\n",
    "\n",
    "We can fit the ecological models to data by defining an estimation engine\n",
    "that contains the method that will be applied for model fitting:\n",
    "\n"
   ]
  }
,
  {
   "cell_type": "code",
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "execution_count": 11, "outputs": [],
   "source": [
    "let engine =\n",
    "    Bristlecone.mkContinuous ()\n",
    "    |\u003e Bristlecone.withCustomOptimisation ( Optimisation.MonteCarlo.Filzbach.filzbach\n",
    "           { Optimisation.MonteCarlo.Filzbach.FilzbachSettings.Default with BurnLength = Optimisation.EndConditions.atIteration 200000\u003citeration\u003e })\n",
    "    |\u003e Bristlecone.withConditioning Conditioning.NoConditioning\n",
    "    |\u003e Bristlecone.withSeed 1500 // We are setting a seed for this example - see below\n",
    "    |\u003e Bristlecone.withTimeConversion DateMode.Conversion.RadiocarbonDates.toYears\n",
    "\n",
    "let endCond = Optimisation.EndConditions.atIteration 100000\u003citeration\u003e\n"
   ]
  }
,
  {
   "cell_type": "markdown",
   "metadata": {},
   
   "source": [
    "Next, we must load in some real data. We leverage FSharp.Data here to read in\n",
    "a csv file containing the raw data.\n",
    "\n"
   ]
  }
,
  {
   "cell_type": "code",
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "execution_count": 12, "outputs": [],
   "source": [
    "open FSharp.Data\n",
    "\n",
    "type PalaeoData = CsvProvider\u003c\"data/loch-dubhan/ld.tsv\"\u003e\n",
    "\n",
    "let data = PalaeoData.Load \"data/loch-dubhan/ld.tsv\"\n",
    "\n",
    "let ts =\n",
    "    [\n",
    "        X.Code, data.Rows |\u003e Seq.map(fun r -\u003e float r.Par_betula, DatingMethods.Radiocarbon (float r.``Scaled_age_cumulative (cal yr bp)`` * 1.\u003cTime.``cal yr BP``\u003e)) |\u003e TimeSeries.fromCalibratedRadiocarbonObservations\n",
    "        obsN.Code, data.Rows |\u003e Seq.map(fun r -\u003e float r.D15N, DatingMethods.Radiocarbon (float r.``Scaled_age_cumulative (cal yr bp)`` * 1.\u003cTime.``cal yr BP``\u003e)) |\u003e TimeSeries.fromCalibratedRadiocarbonObservations\n",
    "    ] |\u003e Map.ofList\n"
   ]
  }
,
  {
   "cell_type": "markdown",
   "metadata": {},
   
   "source": [
    "We can run an individual model fit like so:\n",
    "\n"
   ]
  }
,
  {
   "cell_type": "code",
   "metadata": {
    "dotnet_interactive": {
     "language": "fsharp"
    },
    "polyglot_notebook": {
     "kernelName": "fsharp"
    }
   },
   "execution_count": 13, "outputs": [],
   "source": [
    "let result =\n",
    "    Bristlecone.tryFit engine endCond ts hypotheses.[5].Model\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": ".NET (F#)",
   "language": "F#",
   "name": ".net-fsharp"
  },
  "language_info": {
   "file_extension": ".fs",
   "mimetype": "text/x-fsharp",
   "name": "polyglot-notebook",
   "pygments_lexer": "fsharp"
  },
  "polyglot_notebook": {
   "kernelInfo": {
    "defaultKernelName": "fsharp",
    "items": [
     {
      "aliases": [],
      "languageName": "fsharp",
      "name": "fsharp"
     }
    ]
   }
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}

